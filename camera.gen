/*
 * Copyright (c) 2022-2023 LAAS/CNRS
 *
 * Author: Selvakumar H S - LAAS/CNRS
 *
 * Permission to use, copy, modify, and/or distribute this software for any
 * purpose with or without fee is hereby granted, provided that the above
 * copyright notice and this permission notice appear in all copies.
 *
 * THE SOFTWARE IS PROVIDED "AS IS" AND THE AUTHOR DISCLAIMS ALL WARRANTIES
 * WITH REGARD TO THIS SOFTWARE INCLUDING ALL IMPLIED WARRANTIES OF
 * MERCHANTABILITY AND FITNESS. IN NO EVENT SHALL THE AUTHOR BE LIABLE FOR
 * ANY SPECIAL, DIRECT, INDIRECT, OR CONSEQUENTIAL DAMAGES OR ANY DAMAGES
 * WHATSOEVER RESULTING FROM LOSS OF USE, DATA OR PROFITS, WHETHER IN AN
 * ACTION OF CONTRACT, NEGLIGENCE OR OTHER TORTIOUS ACTION, ARISING OUT OF
 * OR IN CONNECTION WITH THE USE OR PERFORMANCE OF THIS SOFTWARE.
 */

#pragma require "felix-idl"
#pragma require "vision-idl"

#include "or/sensor/camera.gen"

/* -------------------------- MODULE DECLARATION --------------------------- */
component MonocularCamera {
  version "1.0";
  email	  "shasthamsa@laas.fr";
  lang	  "c";
  require "genom3 >= 2.99.26";
  doc 	  "A GenoM module for monocular camera integration.";

  codels-require "opencv4, felix-g3utils, vision-idl";

  exception e_BAD_IMAGE_PORT { short code; string<128> message; };
  exception e_OPENCV_ERROR { short code; string<128> message; };
  exception e_OUT_OF_MEM { short code; string<128> message; };
  exception e_BAD_CONFIG { short code; string<128> message; };

  struct CameraInfo {
        struct size_s {
            unsigned short w, h;
        } size;
        unsigned short frequency;
        string<8> format;   // Y8, Y16, RBG8, RGBA8...
        string<128> device;

        boolean started;
        short compression_rate;
        float hfov;
  };

  /* -------------------------- IDS --------------------------- */
  ids {
    CameraInfo info;

    boolean debug, show_frames;
    octet image_width, image_height;
    octet v_level;
  };

  /* ------------- DEFINITION OF PORTS -------------- */
  // Camera
  port multiple out or::sensor::frame ImageFrame {
    doc "The image frame from monocular camera.";
  };
  port out or::sensor::intrinsics Intrinsics;
  port out or::sensor::extrinsics Extrinsics;

  /* ------------------ TASK DEFINITION -------------------- */
  task publish {
    period 100 ms;
    doc "Publish from Monocular Camera.";

    throw e_OUT_OF_MEM, e_BAD_IMAGE_PORT;
  };

  /* ------------------ ACTIVITY DEFINITION --------------------- */

  activity CameraPublish() {
    doc "Publish from Monocular Camera.";
    after set_device, set_resolution;
    task publish;

    codel<start> camera_start(out ::ids, port out ImageFrame, port out Extrinsics, port out Intrinsics)
        yield wait, pause::start;

    async codel<wait> camera_wait(ids in info.started)
        yield pause::wait, wait, pub;

    codel<pub> camera_publish(ids in info, port out ImageFrame)
        yield wait;

        throw e_OUT_OF_MEM, e_BAD_IMAGE_PORT;
  };

  /* ------------------ SERVICE DEFINITION: Attributes -------------------- */


  /* ------------------ SERVICE DEFINITION: Functions -------------------- */
  function set_debug(in boolean is_debug_mode = FALSE : "Set debug mode (default: false)" ) {
    doc "Set the debug mode.";
    codel SetDebug(local in is_debug_mode, ids out debug);
  };

  function show_image_frames(in boolean show_cv_frames = FALSE : "Show image frames (default: false)" ) {
    doc "Show image frames.";
    codel ShowFrames(local in show_cv_frames, ids out show_frames);
  };

  function set_verbose_level(in octet verbose_level = 0 : "Verbose level") {
    doc "Set the verbose level.";
    codel SetVerboseLevel(local in verbose_level, ids out v_level);
  };

  function set_device(in string<128> device_id = "/dev/video0" : "Device path") {
    doc "Set the device path.";
    codel SetDevice(local in device_id, ids out info.device);
  };

  function set_resolution(in short width = 640 : "Image width", in short height = 480 : "Image height") {
    doc "Set the image resolution.";
    codel SetResolution(local in width, local in height, ids out info.size);
  };

  function set_compression(in short compression_percent = -1 : "Compression ration in percentage (0 - 100) (-1 for no compresstion)") {
    doc "Set the compression ratio.";
    codel SetCompression(local in compression_percent, ids out info.compression_rate);
  };
};